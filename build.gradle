ext {
    gwtVersion = '2.8.2'
    
    jettyVersion = '9.4.11.v20180605'
    springVersion = '5.0.7.RELEASE'
    
    slf4jVersion = '1.8.0-alpha2'
    log4jVersion = '2.10.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

allprojects{
    apply plugin: 'eclipse'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    sourceCompatibility = 9
    targetCompatibility = 9

    def suffix = project.hasProperty('BUILD_RELEASE') ? "" : "-SNAPSHOT"
    version = '2.8.10' + suffix

    publishing {
        publications {
            maven(MavenPublication) {
                groupId 'com.ponysdk'
                artifactId project.name
                from components.java
            }
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}
